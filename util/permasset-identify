#!/usr/bin/perl

use strict;

my $file = undef;

for (my $i=0;$i < @ARGV;) {
    my $a = $ARGV[$i++];

    if ($a =~ s/^-+//) {
        if ($a eq "file") {
            $file = $ARGV[$i++];
        }
        else {
            die "Unknown switch $a";
        }
    }
    else {
        die "Unexpected arg";
    }
}

die "--file spec required" unless defined($file);
die "$file does not exist" unless ( -f $file || -d $file );

my $mime_type = undef;
my $mime_encoding = undef;

# cover for cases the file tool cannot cover
open(T,"<",$file) || die "Cannot open file";
binmode(T);
my $bin;
read(T,$bin,4096);
# RIFF:WEBP for WebP files
if (substr($bin,0,4) eq "RIFF" && substr($bin,8,4) eq "WEBP") {
    $mime_type = "image/webp";
}
# done
close(T);

# AAC/MP3/MP2 "file" is not reliable at identifying these files, unless the MP3 has an ID3v2 tag.
# Also in my archive I've mistaken AAC for MP3 and vice versa.
# Use FFMPEG to identify these files.
# ffprobe has this nice print_format show_format option too!
# QuickTime identification is also unreliable with "file"
if (!defined($mime_type) && $file =~ m/\.(aac|mp1|mp2|mp3|mpg|mpeg|vob|m1v|m2v|mpeg2|mpeg1|ts|mts|m2ts|mov|qt|mp4|3gp|3g2|m4a)$/i) {
    my $format = undef;

    open(T,"-|","ffprobe","-show_format","-print_format","flat","--",$file) || die "Cannot run ffmpeg";
    while (my $raw = <T>) {
        chomp $raw;
        # format.xxx=value or format.xxx="value"
        my $i=index($raw,'=');
        next if $i <= 0;
        my $name = substr($raw,0,$i);
        my $value = substr($raw,$i+1);

        if ($name eq "format.format_name") {
            # NTS: value has quotes i.e "aac" "mp3" etc not aac mp3
            $value =~ s/\"//g;
            $format = $value;
        }
    }
    close(T);

    if ($format eq "aac") {
        $mime_type = "audio/aac";
    }
    elsif ($format eq "mp1" || $format eq "mp2" || $format eq "mp3") {
        $mime_type = "audio/mpeg";
    }
    elsif ($format eq "mpeg" || $format eq "mpegvideo") {
        $mime_type = "video/mpeg";
    }
    elsif ($format eq "mpegts") {
        $mime_type = "video/MP2T";
    }
    elsif ($format =~ m/^mov,/) { # quicktime based formats list multiple, but mov, is first (HACK)
        $mime_type = "video/quicktime";
    }
    elsif ($format =~ m/^matroska,/) { # unix file can identify webm but not mkv? There's no mime type for MKV?
        $mime_type = "video/webm";
    }
}

# use file tool
if (!defined($mime_type)) {
    open(T,"-|","file","--brief","--mime","--",$file) || die "Cannot run file util";
    my $raw = <T>; chomp $raw;
    close(T);

    # if it cannot identify it will just say "data"
    die "Failed to do any identification" unless $raw ne "";

    my @sep = split(/; +/,$raw);
    $mime_type = shift @sep if @sep > 0;
    $mime_encoding = shift @sep if @sep > 0;
}

die "Failed to obtain MIME type" unless $mime_type ne "";

print "$mime_type\n";

